#!/usr/bin/perl
#
# Show unreplied mail - gene@cpan.org
# Created:  02/10/05
#
# Possible future enhancements:  Use Getopt::Std and accept command
#   line options for user, maildir, box size thresholds, box name
#   exclusion, decode value, debugging, headers to show, headers to
#   test against (e.g. x-status), header test value (e.g. A), sorting,
#   basic stats, date/time constraints, header text RE constraint,
#   etc.
#
use strict;
use warnings;

use Mail::MboxParser;

die "Usage: perl $0 [-h] [-n] [moe larry curly shemp]\n"
    if $ARGV[0] eq '-h';

# Accept a list of folders.
my @folders = @ARGV;

# Do we just want to show the names and sizes of the folders?
my $name_only;
if ($folders[0] eq '-n') {
    $name_only = 1;
    shift @folders;
}

# Set the path of the mail directory.
my $path = '/home/gene/mail';

# Open the mail directory.
opendir MAIL, $path or die "Can't open $path for reading: $!\n";

# Loop over all the mail folders.
for my $box (grep { /^[^.]/o } readdir MAIL) {
    # Skip if not in the folder list, if one was provided.
    next if @folders && !grep { /$box/ } @folders;

    # What box are we going to look in?
    print "$box: ";

    # Instantiate a new object for the current box, and we only care
    # about the headers.
    $box = Mail::MboxParser->new("$path/$box", decode => 'HEADER');

    # How many messages are in this box?
    print $box->nmsgs, "\n";

    # Skip if we only need to show the box name and size.
    next if $name_only;

    # Loop over the box messages.
    my $i = 0;
    while (my $msg = $box->next_message) {
#        print Data::Dumper::Dumper($msg->header), "\n";
        # Only show the messages that have not been replied to.
        unless ($msg->header->{'x-status'} eq 'A') {
            # Grab the interesting bits.
            my $date    = $msg->header->{date}    || 'No date';
            my $name    = $msg->header->{name}    || 'No name';
            my $subject = $msg->header->{subject} || 'No subject';

            # Show the interesting bits.
            printf "%d. %s - %s\n\t%s\n", ++$i, $date, $name, $subject;
        }
    }

    # Show a terminator for the box.
    print '-' x 50, "\n";
}

closedir MAIL;
